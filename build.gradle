buildscript {
    repositories {
        maven { url("https://maven.aliyun.com/repository/public") }
        maven { url("https://maven.aliyun.com/repository/spring") }
        maven { url("https://maven.aliyun.com/repository/mapr-public") }
        maven { url("https://maven.aliyun.com/repository/spring-plugin") }
        maven { url("https://maven.aliyun.com/repository/gradle-plugin") }
        maven { url("https://maven.aliyun.com/repository/google") }
        maven { url("https://maven.aliyun.com/repository/jcenter") }
    }
}

plugins {
    id "de.undercouch.download" version "4.1.1"
}

def mainVersion = "1.2.0"
Map<String, String> envMap = new HashMap<>(System.getenv())
def imageRepository = envMap.getOrDefault("IMAGE_REPOSITORY ", "wangz2019/docker-novnc")
def centosVersionList = ["centos8.3.2011"]
def archList = ["amd64", "arm64"]
File runtimeDockerDirectory = project.file("${project.buildDir}/runtime/docker")

task buildDockerImage() {
    doFirst {
        runtimeDockerDirectory.mkdirs()
        runtimeDockerDirectory.delete()
        copy {
            from project.file("docker")
            into runtimeDockerDirectory
        }
        download {
            src "https://github.com/novnc/noVNC/archive/refs/tags/v1.2.0.tar.gz"
            dest runtimeDockerDirectory
            tempAndMove true
            overwrite false
        }
    }
    doLast {
        for (String centosVersion : centosVersionList) {
            for (String arch : archList) {
                String imageWithTag = imageWithTag(imageRepository, mainVersion, centosVersion, arch)
                exec {
                    commandLine(
                            "docker", "buildx", "build",
                            "--platform", "linux/${arch}",
                            "--rm", runtimeDockerDirectory,
                            "-f", project.file("${runtimeDockerDirectory.getAbsolutePath()}/Dockerfile"),
                            "-t", imageWithTag,
                            "--build-arg", "CENOS_VERSION=${centosVersion}",
                    )
                }
            }
        }
    }
}

task pushDockerImage() {
    doLast {
        for (String centosVersion : centosVersionList) {
            for (String arch : archList) {
                String imageWithTag = imageWithTag(imageRepository, mainVersion, centosVersion, arch)
                exec {
                    commandLine("docker", "push", imageWithTag)
                }
            }
        }
    }
    dependsOn(buildDockerImage)
}

def containerName = "docker-novnc"
def port = 6080
task runDockerContainer(type: Exec) {
    def currentArch = currentArch()
    doFirst {
        println("running command: ${String.join(" ", getCommandLine())}")
    }
    executable("docker")
    args(
            "run", "--rm",
            "--add-host", "host.docker.internal:host-gateway",
            "-p", "${port}:6080",
            "--platform=linux/${currentArch}",
            "-e", "VNC_SERVER=host.docker.internal:5901",
            "--name", containerName,
            "-d", imageWithTag(imageRepository, mainVersion, centosVersionList.get(0), currentArch),
    )
    doLast {
        println("visit http://localhost:${port}/")
    }
}

task stopDockerContainer(type: Exec) {
    executable("docker")
    args(
            "stop", containerName
    )
}

private static String imageWithTag(
        String imageRepository,
        String mainVersion,
        String centosVersion,
        String arch
) {
    return "${imageRepository}:${mainVersion}-${centosVersion}-linux-${arch}"
}

private static String currentArch() {
    String arch = System.getProperty("os.arch")
    switch (arch) {
        case "x86":
            return "i386"
        case "x86_64":
            return "amd64"
        case "amd64":
        case "arm64":
            return arch
        default:
            throw new RuntimeException("not supported arch: ${arch}")
    }
}

apply from: project.file("docs.nginx.gradle")
